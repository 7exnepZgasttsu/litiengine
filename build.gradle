buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
  }
}
 
 
plugins {
  id 'com.stehno.natives' version '0.3.1'
  id "org.sonarqube" version "2.5"
}

natives {
  configurations = ['runtime']
  outputDir = 'libs'
}

apply plugin: 'java'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'maven'

archivesBaseName = "litiengine"
version = "v0.4.12-alpha"

sourceSets {
  main.java.srcDir "src"
  main.java.srcDir "resources"
  
  test {
    java {
      srcDirs = ["tests"] 
    }
  }
}

repositories {
   mavenCentral()
}

dependencies {
  compile 'net.java.jinput:jinput:2.0.7'
  compile 'com.googlecode.soundlibs:jorbis:0.0.17.4'
  compile 'com.googlecode.soundlibs:tritonus-share:0.3.7.4'
  compile 'com.googlecode.soundlibs:vorbisspi:1.0.3.3'
  compile ('com.googlecode.soundlibs:mp3spi:1.9.5.4'){
    exclude group:'junit'
  }

  compile 'com.code-disaster.steamworks4j:steamworks4j:1.7.0'
  
  testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.+'
  testCompile 'org.junit.jupiter:junit-jupiter-params:5.1.+'
  testCompile 'org.mockito:mockito-core:2.+'
  testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.+'
  testRuntime 'org.junit.platform:junit-platform-launcher:1.1.+'
}

jar {
  from  {configurations.compile.collect { zipTree it }
  } {
     exclude 'META-INF/services/**'
  }

  from('resources') { include '**/*' }
  
  exclude '**/*.dll'
  exclude '**/*.jnilib'
  exclude '**/*.dylib'
  exclude '**/*.so'
  exclude 'junit**/**'
}

javadoc {
  options.encoding 'UTF-8'
}

afterEvaluate {
  def junitPlatformTestTask = (JavaExec) project.tasks.getByName('junitPlatformTest')

  jacoco {
    applyTo(junitPlatformTestTask)
  }

  project.task(type: JacocoReport, "jacocoJupTestReport") {
    executionData(junitPlatformTestTask)
    sourceSets(sourceSets.main)
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)

    reports {
      xml.enabled true
      html.enabled true
      xml.destination file("${buildDir}/jacoco/report.xml")
      html.destination file("${buildDir}/jacoco/html")
    }
  }

  fullbuild.dependsOn jacocoJupTestReport
}

task createJavadocs(type: Javadoc) {
  source sourceSets.main.allJava
  classpath = configurations.compile
  options.encoding 'UTF-8'
  dependsOn build
}

task distZip(type: Zip) {
  group 'build'
  from 'build/libs/'
  include '*'
  archiveName archivesBaseName + '-' + version + '.zip'
  exclude archiveName
  destinationDir(file('build/libs/'))
}

task fullbuild{
  group 'build'
  description 'executes the build including coverage reports and javadoc'
  dependsOn createJavadocs
}

task copyLicense(type: Copy) { 
  from(new File('.')) { include 'LICENSE' }
  into new File(buildDir, 'libs')
}

fullbuild.dependsOn distZip
fullbuild.dependsOn includeNatives
build.dependsOn includeNatives
includeNatives.dependsOn copyLicense

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}