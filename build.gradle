plugins {
    id "org.sonarqube" version "2.5"
}  

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceSets {
  main.java.srcDir "src"
  main.java.srcDir "resources"
  
  test {
    java {
      srcDirs = ["tests"] 
    }
  }
}

repositories {
   mavenCentral()
   jcenter()
   flatDir {
      dirs 'lib'
   }
}

dependencies {
  compile name: 'jinput'
  compile name: 'jogg-0.0.7'
  compile name: 'jorbis-0.0.15'
  compile name: 'tritonus_share'
  compile name: 'vorbisspi1.0.3'
  compile name: 'jl1.0.1'
  compile name: 'mp3spi1.9.5'
  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.+'
}

jar {
  from configurations.compile.collect { zipTree it }
  
  from('resources') { include '**/*' }
  version = 'v0.4.6-alpha'
}

javadoc {
  options.encoding = 'UTF-8'
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
  
   afterEvaluate {
      classDirectories = files(classDirectories.files.collect {
          fileTree(dir: it,
                  exclude: ['tests/**'])
                  
      })
  }
}

check.dependsOn jacocoTestReport


task copyNativeLibs(type: Copy) {
  from(new File('lib')) { exclude '**/*.jar' }
  into new File(buildDir, 'libs')
  
  from(new File('.')) { include 'LICENSE' }
  into new File(buildDir, 'libs')
}

task createJavadocs(type: Javadoc) {
  source = sourceSets.main.allJava
  options.encoding = 'UTF-8'
  dependsOn build
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    dependsOn copyNativeLibs
}

